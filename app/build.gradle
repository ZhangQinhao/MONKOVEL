apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'

static def increasedVersionCode(){
    return (int)(System.currentTimeMillis()/1000/60/60-17141)
}
def name = "MONKOVEL"
def version = "1.4." + increasedVersionCode()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.gedoor.monkeybook"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 10000 + gitCommits
        versionName version
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "debug"]
        project.ext.set("archivesBaseName", name + "_" + version)
    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            buildConfigField "boolean", "IS_RELEASE", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
            buildConfigField "boolean", "IS_RELEASE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${name}_${defaultConfig.versionName}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
ext {
    support_library_version = '26.1.0'
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    //firebase
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    //support
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:support-compat:$support_library_version"
    implementation 'com.google.code.gson:gson:2.8.0'
    api project(':basemvplib')
    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.0.1'
    //JSOUP
    implementation 'org.jsoup:jsoup:1.10.2'
    //沉浸式状态栏
    implementation 'com.zhangmonke:ImmerseLayout:1.1.1'
    //ProgressBar
    implementation 'com.zhangmonke:MProgressBar:1.0.1'
    //Glide
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    //AutofitTextView
    implementation 'me.grantland:autofittextview:0.2.1'
    //删除粒子效果
    implementation 'tyrantgit:explosionfield:1.0.1'
    //View简易动画
    implementation 'com.daimajia.easing:library:2.0@aar'
    implementation 'com.daimajia.androidanimations:library:2.2@aar'
    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    //SwitchButton
    implementation 'com.kyleduo.switchbutton:library:1.4.4'
    //bind view
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.victor:lib:1.0.4'
    //关于页面
    implementation 'com.github.medyo:android-about-page:1.2.2'
    //简易权限获取
    implementation 'pub.devrel:easypermissions:1.1.1'
}

greendao {
    schemaVersion 3
    daoPackage 'com.monke.monkeybook.dao'
    targetGenDir 'src/main/java'
}

apply plugin: 'com.google.gms.google-services'